System rover

Event cmd : cmd(X)
Event inputcmd : usercmd(executeInput(X))
Event usercmd : usercmd(robotgui(X))
Event sonar : sonar(SONARNAME, TARGETNAME, DISTANCE)
Event sonarDetect : sonarDetect(X) // sonar on rover
Event alarm : alarm(X)

Event unityAddr : unityAddr(X)
Event startDistance : startDistance(X)
Event sonarDistance : sonarDistance( SONARID, DISTANCE )

Dispatch moveRover : cmd( CMD )

Context ctxRover ip [ host="localhost" port=8090 ] -httpserver

EventHandler evh for  sonar { 
	memoCurrentEvent -lastonly for sonarhandler
} ;

QActor sonarhandler context ctxRover {
	Rules{   
		lastSonarData( SONARID ):-
	 			retract( msg(M,EV,EMITTER,none,sonar(SONARID,TARGET,DISTANCE),N) ),
				replaceRule(  sonardata( sonar(SONARID,_,_)  ),
				sonardata( sonar(SONARID,TARGET,DISTANCE))
			),
			lastSonarData(SONARID).
		lastSonarData( SONARID ).
		
		updateSonarData :-
			lastSonarData(sonar1),
			lastSonarData(sonar2).
			
		isnear(SONARID,DISTANCE) :- 
			sonardata( sonar(SONARID,TARGET,DISTANCE) ),
			eval( lt, DISTANCE, 40).
				
		isnearer(SONARID) :-
			sonardata( sonar(SONARID,TARGET,DISTANCE) ),
			startingDistance(GOALDISTANCE),
			eval( lt, DISTANCE, GOALDISTANCE),
			eval( minus, GOALDISTANCE, DISTANCE, R ),
			eval( gt, R, 2).
			
		isfarther(SONARID) :-
			sonardata( sonar(SONARID,TARGET,DISTANCE) ),
			startingDistance(GOALDISTANCE),
			eval( gt, DISTANCE, GOALDISTANCE),
			eval( minus, DISTANCE, GOALDISTANCE, R ),
			eval( gt, R, 2).
			
		isatdistance(SONARID) :-
			sonardata( sonar(SONARID,TARGET,DISTANCE) ),
			startingDistance(GOALDISTANCE),
			eval( minus, GOALDISTANCE, 2, LOWERTHREASHOLD ),
			eval( plus, GOALDISTANCE, 2, UPPERTHREASHOLD ),
			eval( gt, DISTANCE, LOWERTHREASHOLD ),
			eval( lt, DISTANCE, UPPERTHREASHOLD ).		
	}
	
	Plan init normal
		actions [
			
		]
		switchTo handleEvents
	
	Plan handleEvents
		actions[   	
		]
	    transition 
			whenTime 60000 -> handleTout,
	    	whenEvent sonar -> processSonarData,
	    	whenEvent startDistance -> setStartDistance,
	    	whenEvent alarm -> handleAlarm
	   	finally repeatPlan
		
	Plan setStartDistance resumeLastPlan
		actions [
			println("setStartDistance");
			removeRule startingDistance(X);
			onEvent startDistance : startDistance(X) -> addRule startingDistance(X)
		]
   	
   	Plan processSonarData resumeLastPlan
	   	actions[
	   		demo updateSonarData
	   	]
   		switchTo checkDistance 
    
    Plan checkDistance resumeLastPlan
    actions[
   		// Se manca startingDistance ovvero al primo avvio, impostalo al valore attuale rilevato
   		[ not !? startingDistance(X) ] onEvent sonar : sonar(sonar1,TARGET,DISTANCE) -> addRule startingDistance(DISTANCE);   		
   		onEvent sonar : sonar(sonar1,TARGET,DISTANCE) -> { // Siamo davanti al sonar1 ?
   			[ !? isatdistance(sonar1) ] { // Siamo alla distanza corretta?
	   			// Se il rover si sta muovendo gestito dal sonar, fermiamolo
   				[ ?? adjustingDistance ] forward rover -m moveRover : cmd(move("stop",40,0))	
   			} else {
   				[ not !? adjustingDistance ] { // Il rover parte non gestito dal sonar, prendiamone possesso
					[ !? isfarther(sonar1) ] forward rover -m moveRover : cmd(move("forward",20,0));
      				[ !? isnearer(sonar1) ] forward rover -m moveRover : cmd(move("backward",20,0));
					addRule adjustingDistance
   				}
   			}
   		} else {   		
	   		onEvent sonar : sonar(sonar2,TARGET,DISTANCE) -> {
	   			[ !? isatdistance(sonar2) ] {
	   				[ ?? adjustingDistance ] {
	   					forward rover -m moveRover : cmd(move("stop",40,0))
	   				} else {
	   					emit alarm : alarm(sonar2) // Ferma il rover
	   				}
	   			} else {
	   				[ not !? adjustingDistance ] {
	   					emit alarm : alarm(sonar2); // Ferma il rover
	   					forward rover -m moveRover : cmd(move("left",40,1000));
						[ !? isfarther(sonar2) ] forward rover -m moveRover : cmd(move("forward",20,0))
							else forward rover -m moveRover : cmd(move("backward",20,0));
						addRule adjustingDistance
	   				}
	   			}
	   		} else {
	   			// Il sonar ha il controllo del rover ma non lo rileva: siamo finiti fuori dal suo campo!
	   			[ ?? adjustingDistance ] { 
	   				forward rover -m moveRover : cmd(move("stop",100,0));
	   				forward rover -m moveRover : cmd(move("right",40,1000));
	   				forward rover -m moveRover : cmd(move("backward",1,0))
	   			}
	   		}
 
   		}
   	]
   	
   	Plan handleTout resumeLastPlan
   		actions []
   		
   	Plan handleAlarm resumeLastPlan
   		actions [
   			onEvent alarm : alarm(fire) -> {
   				[ ?? adjustingDistance] println("[sonarhandler] detected fire alarm while I had control, cleared out.")
   			}
   		]
}

QActor rover context ctxRover {

	Plan init normal
		actions []
		switchTo handleCommands
	
	Plan handleCommands
		actions []
		transition 
			whenTime 60000 -> handleTout,
			whenEvent unityAddr -> connectToUnity,
			whenMsg moveRover -> execMove,
			whenEvent alarm -> handleAlarm
		finally repeatPlan
		
	Plan connectToUnity resumeLastPlan 
		actions [
			memoCurrentEvent
			else {
				onEvent unityAddr:unityAddr(X) -> connectUnity "X";
				actorOp createSimulatedActor;				
				addRule unityOn
			}
		]
			
	// movimento del rover e registrazione del movimento
	Plan execMove resumeLastPlan
		actions [
			onMsg moveRover : cmd(move("forward",SPEED,DURATION)) -> {
				onward 20 time (0);
				javaRun it.unibo.custom.path.register("forward", DURATION)				
			};
			onMsg moveRover : cmd(move("backward",SPEED,DURATION)) -> {
				backwards 20 time (0);
				javaRun it.unibo.custom.path.register("backward", DURATION)	
			};
			onMsg moveRover : cmd(move("stop",SPEED,DURATION)) -> {
				stop 20 time (750);
				javaRun it.unibo.custom.path.register("stop", DURATION)	
			};
			onMsg moveRover : cmd(move("left",SPEED,DURATION)) -> {
				left 20 time (750);
				javaRun it.unibo.custom.path.register("left", DURATION)
			};
			onMsg moveRover : cmd(move("right",SPEED,DURATION)) -> {
				right 20 time (750);
				javaRun it.unibo.custom.path.register("right", DURATION)				
			};			
			onMsg moveRover : cmd(X) -> println(X)
		]
	
	Plan handleTout resumeLastPlan
		actions []
		
	Plan handleAlarm resumeLastPlan
		actions [
			stop 40 time (0) 
		]
		
	Plan endOfMove resumeLastPlan []
}

QActor rovermind context ctxRover {
	Plan init normal
		actions []
		switchTo handleCommands
	
	Plan handleCommands
		actions []
		transition
			whenTime 60000 -> handleTout,
			whenMsg moveRover -> execMove
		finally repeatPlan
		
	Plan execMove resumeLastPlan
		actions [
			onMsg moveRover : cmd(start) -> {
				addRule run;
				forward rover -m moveRover : cmd(move("right",40,750))
			}
		]
		switchTo [ !? run ] stage1
		
	Plan stage1[
		[?? run]{
			forward rover -m moveRover : cmd(move("forward",20,0))
		}
	]
	transition stopAfter 25000
		whenEvent alarm-> handleAlarm,
		whenEvent sonarDetect -> stage1_obstacleAvoidingManagement //SE QUESTO SCATTA: fixed or dynamic

	finally repeatPlan
	
	Plan stage1_repeat resumeLastPlan[
		addRule run
	]

	Plan stage1_obstacleAvoidingManagement resumeLastPlan [
		addRule run;
		forward rover -m moveRover : cmd(move("stop",20,0));
		delay 1500
	]
	transition
		whenTime 500 -> stage1_evaluateDetection,
		whenEvent alarm->handleAlarm
		
	Plan stage1_evaluateDetection []
	transition
		whenTime 500 -> stage1
		whenEvent alarm->handleAlarm,
		whenEvent sonarDetect->stage1_managingFixedObject_loop_begin
		
	Plan stage1_managingFixedObject_loop_begin[
		onEvent sonarDetect:sonarDetect(NAME) ->println(detected(NAME));
		forward rover -m moveRover : cmd(move("backward",20,1000))
	]
	transition 
		whenTime 1000 -> stage1_managingFixedObject_loop_firstRotation,
		whenEvent alarm -> handleAlarm
	
	Plan stage1_managingFixedObject_loop_firstRotation [
		[not !? leftEdge] forward rover -m moveRover : cmd(move("left",40,750))
		else forward rover -m moveRover : cmd(move("right",40,750))
	]
	switchTo stage1_managingFixedObject_loop_avoiding
	
	Plan stage1_managingFixedObject_loop_avoiding [
		forward rover -m moveRover : cmd(move("forward",20,0))
	]
	transition
		whenTime 2000 -> stage1_managingFixedObject_endloop,
		whenEvent alarm->handleAlarm,
		whenEvent [not !? leftEdge] sonarDetect->stage1_managingFixedObject_loop_edgeDetected
	finally repeatPlan
	
	Plan stage1_managingFixedObject_loop_edgeDetected [
		forward rover -m moveRover : cmd(move("stop",40,0));
		addRule edgeDetected;
		[not !? leftEdge]
			addRule leftEdge
		else
			removeRule leftEdge
	] 
	transition
		whenTime 400 -> stage1_managingFixedObject_loop_edgeDetected_backward // Durata della retro-traslazione laterale per obstacleAvoidance
		whenEvent alarm -> handleAlarm
	
	Plan stage1_managingFixedObject_loop_edgeDetected_backward [
		forward rover -m moveRover : cmd(move("backward",20,1000))
	]
	transition
		whenTime 1000 -> stage1_managingFixedObject_endloop,
		whenEvent alarm -> handleAlarm
	
	Plan stage1_managingFixedObject_endloop[
		// caso che si verifica soltanto in cui il rover sta andando a sinistra (per obstacleAvoidance) e si trova un ostacolo davanti
		[?? edgeDetected] {
			[!? leftEdge]	forward rover -m moveRover : cmd(move("right",40,750))
			else forward rover -m moveRover : cmd(move("left",40,750))
		}
		else {
			[not !? leftEdge] forward rover -m moveRover : cmd(move("right",40,750))
			else forward rover -m moveRover : cmd(move("left",40,750))
		}
		
	]
	switchTo stage1
	
	Plan stage2 
		actions []
		transition
			whenTime 60000 -> handleTout,
			whenEvent sonarDetect -> stage1_obstacleAvoidingManagement //se questo event viene generato, allora siamo davanti a un obstacle
	
	Plan handleAlarm
		actions [
			onEvent alarm : alarm(fire) ->	addRule manual			
		]
		switchTo [?? manual] stage2
	
	Plan handleTout resumeLastPlan
		actions []
}

QActor console context ctxRover {
	Plan init normal
		actions []
		switchTo handleCommands
	
	Plan handleCommands
		actions []
		transition 
			whenTime 60000 -> handleTout,
			whenEvent cmd -> handleCmd,
			whenEvent inputcmd -> handleInputCmd,
			whenEvent usercmd -> handleUserCmd
		finally repeatPlan
		
	Plan handleCmd resumeLastPlan
		actions [
			onEvent cmd : cmd(start) -> forward rovermind -m moveRover : cmd(start) 
		]
	
	Plan handleUserCmd resumeLastPlan
		actions [
			onEvent usercmd : usercmd(robotgui(w(X))) -> forward rover -m moveRover : cmd(move("forward", 40, 0));
			onEvent usercmd : usercmd(robotgui(a(X))) -> forward rover -m moveRover : cmd(move("left", 50, 750));
			onEvent usercmd : usercmd(robotgui(s(X))) -> forward rover -m moveRover : cmd(move("backward", 40, 0));
			onEvent usercmd : usercmd(robotgui(d(X))) -> forward rover -m moveRover : cmd(move("right", 50, 750));
			onEvent usercmd : usercmd(robotgui(h(X))) -> forward rover -m moveRover : cmd(move("stop", 40, 0));
			onEvent usercmd : usercmd(robotgui(X)) -> println(X)
		]
	
	Plan handleInputCmd resumeLastPlan
		actions [
			onEvent inputcmd : usercmd(executeInput(connectToUnity(X))) -> emit unityAddr : unityAddr(X) ;
			onEvent inputcmd : usercmd(executeInput(setStartDistance(X))) -> emit startDistance : startDistance(X);
			onEvent inputcmd : usercmd(executeInput(move(X,Y,Z))) -> forward rover -m moveRover : cmd(move(X,Y,Z));
			onEvent inputcmd : usercmd(executeInput(X)) -> println(X)
		]
		
	Plan handleTout resumeLastPlan
		actions []
}